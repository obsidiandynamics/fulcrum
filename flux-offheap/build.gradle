def packageName = "fulcrum-flux-offheap"
version = project(":").version

dependencies {
  api project(":fulcrum-flux")
  api project(":fulcrum-format")
  api project(":fulcrum-fslock")
  api project(":fulcrum-io")
  api project(":fulcrum-random")
  
  compileOnly "net.openhft:chronicle-queue:${chronicleQueueVersion}"
  compileOnly "com.esotericsoftware:kryo:${kryoVersion}"

  testImplementation project(":fulcrum-assert")
  testImplementation project(":fulcrum-await")
  testImplementation project(":fulcrum-junit")
  testImplementation project(":fulcrum-verifier")
  testImplementation project(":fulcrum-flux").sourceSets.test.output
  testImplementation "net.openhft:chronicle-queue:${chronicleQueueVersion}"
  testImplementation "com.esotericsoftware:kryo:${kryoVersion}"

  testImplementation "org.slf4j:slf4j-nop:${slf4jVersion}"
}

jar {
  baseName packageName
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  baseName packageName
  classifier = "javadoc"
  from "$buildDir/docs/javadoc"
}
 
task sourcesJar(type: Jar) {
  baseName packageName
  from sourceSets.main.allSource
  classifier = "sources"
}
 
artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project(":").group
      artifactId packageName
      version project(":").version
      from components.java
      artifact sourcesJar
      artifact javadocJar
    }
  }
  repositories {
    maven {
      url "${project(':').projectDir}/../repo"
    }
  }
}

def envUser = "BINTRAY_USER"
def envKey = "BINTRAY_KEY"
def packageDesc = "Off-heap support for Flux"
def repoName = "fulcrum"
bintray {
  user = System.getenv(envUser)
  key = System.getenv(envKey)
  publications = ["mavenJava"]
  pkg {
    repo = "${repoName}"
    name = packageName
    userOrg = "obsidiandynamics"
    desc = packageDesc
    websiteUrl = "https://github.com/obsidiandynamics/${repoName}"
    licenses = ["BSD New"]
    vcsUrl = "https://github.com/obsidiandynamics/${repoName}"
    issueTrackerUrl = "https://github.com/obsidiandynamics/${repoName}/issues"
    publicDownloadNumbers = true
    githubRepo = "obsidiandynamics/${repoName}"
    override = true
    publish = true
    version {
      name = project(":").version
      desc = packageDesc
      released  = new Date()
      vcsTag = project(":").version
    }
  }
}
