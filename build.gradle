plugins {
  id "com.jfrog.bintray" version "1.8.4"
  id "com.github.ben-manes.versions" version "0.21.0"
}
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

group = "com.obsidiandynamics.fulcrum"
version = "0.35.0-SNAPSHOT"
  
def envUser = "BINTRAY_USER"
def envKey = "BINTRAY_KEY"
task bintrayCredentialsCheck {
  doLast {
    if (System.getenv(envUser) == null) {
      throw new GradleException("No Bintray username specified; set with 'export ${envUser}=<username>'")
    }
    if (System.getenv(envKey) == null) {
      throw new GradleException("No Bintray key specified; set with 'export ${envKey}=<key>'")
    }
  }
}

def baseJvmArgs = [
  "--add-opens=java.base/java.io=ALL-UNNAMED",
  "--add-opens=java.base/java.lang=ALL-UNNAMED",
  "--add-opens=java.base/java.lang.invoke=ALL-UNNAMED",
  "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED",
  "--add-opens=java.base/java.nio=ALL-UNNAMED",
  "--add-opens=java.base/java.util=ALL-UNNAMED",
  "--add-opens=java.base/java.util.concurrent.atomic=ALL-UNNAMED",
  "--add-opens=java.base/sun.nio.ch=ALL-UNNAMED",
  "-XX:-MaxFDLimit"
]
project.ext.set("baseJvmArgs", baseJvmArgs)

allprojects {
  apply plugin: "java"
  apply plugin: "jacoco"
  apply plugin: "maven-publish"
  apply plugin: "com.jfrog.bintray"
  
  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    jcenter()
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots"
    }
  }
  
  ext {
    antVersion = "1.10.6"
    assertjVersion = "3.12.2"
    chronicleQueueVersion = "5.17.14"
    equalsverifierVersion = "3.1.9"
    glassfishELVersion = "3.0.1-b11"
    hamcrestVersion = "2.1"
    hibernateValidatorVersion = "6.0.13.Final"
    httpasyncclientVersion = "4.1.4"
    httpclientVersion = "4.5.9"
    jacksonVersion = "2.9.9"
    jgroupsVersion = "4.1.1.Final"
    jmhVersion = "1.21"
    junitVersion = "4.12"
    kryoVersion = "5.0.0-RC4"
    log4jVersion = "1.2.17"
    mockitoVersion = "3.0.0"
    pojotesterVersion = "0.9.0"
    slf4jVersion = "1.7.25"
    undertowVersion = "2.0.22.Final"
    validationApiVersion = "2.0.1.Final"
    wiremockVersion = "2.23.2"
  }
  
  dependencies {
    testCompile "junit:junit:${junitVersion}"
    testCompile "com.obsidiandynamics.pojotester:pojotester:${pojotesterVersion}"
    testCompile "nl.jqno.equalsverifier:equalsverifier:${equalsverifierVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testCompile "org.assertj:assertj-core:${assertjVersion}"
    testCompile "org.hamcrest:hamcrest-library:${hamcrestVersion}"
  }
  
  compileJava {
    options.compilerArgs += "-proc:none"
    options.compilerArgs += "-Xlint:unchecked"
  }

  compileTestJava {
    options.compilerArgs += "-proc:none"
    options.compilerArgs += "-Xlint:unchecked"
  }
  
  tasks.withType(JavaCompile) {
    options.fork = true
    options.incremental = true
  }

  test {
    exclude "**/*IT.class"
    jvmArgs += baseJvmArgs
  }
  
  task integrationTest(type: Test, description: "Runs integration tests", group: "Verification") {
    include "**/*IT.class"
    jvmArgs += baseJvmArgs
  }

  jacoco {
    toolVersion = "0.8.4"
  }
  
  task verify {  
    dependsOn test
    dependsOn integrationTest
  }

  jacocoTestReport {
    additionalSourceDirs.from = files(sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(sourceSets.main.output)
    executionData.from = files("$buildDir/jacoco/test.exec") + files("$buildDir/jacoco/integrationTest.exec")
    reports {
      html.enabled true
      xml.enabled true
      csv.enabled false
    }
  }

  javadoc {
    if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
      options.addStringOption("quiet", "-html5")
    }
  }

  // invoke this task when ready to publish to Bintray
  bintrayUpload {
    dependsOn ":bintrayCredentialsCheck"
    dependsOn "jar"
    dependsOn "sourcesJar"
    dependsOn "javadocJar"
    dependsOn "generatePomFileForMavenJavaPublication"
  }
}

subprojects {
}

task jacocoRootReport(type: JacocoReport) {
  mustRunAfter = allprojects.test + allprojects.integrationTest
  additionalSourceDirs.from = files(allprojects.sourceSets.main.allSource.srcDirs)
  sourceDirectories.from = files(allprojects.sourceSets.main.allSource.srcDirs)
  classDirectories.from = files(allprojects.sourceSets.main.output)
  executionData.from = files(allprojects.jacocoTestReport.executionData)
  reports {
    html.enabled true
    xml.enabled true
    csv.enabled false
  }
  onlyIf = {
    true
  }
  doFirst {
    executionData.from = files(executionData.findAll {
      it.exists()
    })
  }
  afterEvaluate {
    classDirectories.from = files(classDirectories.files.collect {
      fileTree(dir: it, exclude: ["sample/**", "**/*Uncovered*", "**/generated/**"])
    })
  }
}

def packageName = "fulcrum-core"

jar {
  baseName packageName
  finalizedBy jacocoRootReport
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  baseName packageName
  classifier = "javadoc"
  from "$buildDir/docs/javadoc"
}
 
task sourcesJar(type: Jar) {
  baseName packageName
  from sourceSets.main.allSource
  classifier = "sources"
}
 
artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project(":").group
      artifactId packageName
      version project(":").version
      from components.java
      artifact sourcesJar
      artifact javadocJar
    }
  }
  repositories {
    maven {
      url "${project(':').projectDir}/../repo"
    }
  }
}

def packageDesc = "Core package"
def repoName = "fulcrum"
bintray {
  user = System.getenv(envUser)
  key = System.getenv(envKey)
  publications = ["mavenJava"]
  pkg {
    repo = "${repoName}"
    name = packageName
    userOrg = "obsidiandynamics"
    desc = packageDesc
    websiteUrl = "https://github.com/obsidiandynamics/${repoName}"
    licenses = ["BSD New"]
    vcsUrl = "https://github.com/obsidiandynamics/${repoName}"
    issueTrackerUrl = "https://github.com/obsidiandynamics/${repoName}/issues"
    publicDownloadNumbers = true
    githubRepo = "obsidiandynamics/${repoName}"
    override = true
    publish = true
    version {
      name = project(":").version
      desc = packageDesc
      released  = new Date()
      vcsTag = project(":").version
    }
  }
}
