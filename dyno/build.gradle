def packageName = "fulcrum-dyno"
version = project(":").version

repositories {
  jcenter()
}

buildscript {
  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'net.ltgt.gradle:gradle-apt-plugin:0.15'
  }
}

apply plugin: 'net.ltgt.apt'

dependencies {
  compile project(":fulcrum-func")
  compile 'org.openjdk.jmh:jmh-core:1.19'
  annotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.19'
}

compileJava {
  aptOptions.annotationProcessing = false
}
    
task copySrc(type: Copy, dependsOn: compileJava) {
  from 'build/generated/source/apt/main/com/obsidiandynamics/dyno/generated'
  into 'src/main/java/com/obsidiandynamics/dyno/generated'
  include '*.java'
}
    
task copyMetaInf(type: Copy, dependsOn: compileJava) {
  from 'build/classes/java/main/META-INF'
  into 'src/main/resources/META-INF'
  include '*'
}

task enableAnn {
  doLast {
    tasks.withType(JavaCompile) {
      aptOptions.annotationProcessing = true
    }
  }
}

task gen(dependsOn: [enableAnn, compileJava, copySrc, copyMetaInf]) {
  // the dependsOn order is non-deterministic; set the task dependencies explicitly
  compileJava.mustRunAfter enableAnn
}

jar {
  baseName packageName
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  baseName packageName
  classifier = "javadoc"
  from "$buildDir/docs/javadoc"
}
 
task sourcesJar(type: Jar) {
  baseName packageName
  from sourceSets.main.allSource
  classifier = "sources"
}
 
artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project(":").group
      artifactId packageName
      version project(":").version
      from components.java
      artifact sourcesJar
      artifact javadocJar
    }
  }
  repositories {
    maven {
      url "${project(':').projectDir}/../repo"
    }
  }
}

def envUser = "BINTRAY_USER"
def envKey = "BINTRAY_KEY"
def packageDesc = "Microbenchmarking harness"
def repoName = "fulcrum"
bintray {
  user = System.getenv(envUser)
  key = System.getenv(envKey)
  publications = ["mavenJava"]
  pkg {
    repo = "${repoName}"
    name = packageName
    userOrg = "obsidiandynamics"
    desc = packageDesc
    websiteUrl = "https://github.com/obsidiandynamics/${repoName}"
    licenses = ["BSD New"]
    vcsUrl = "https://github.com/obsidiandynamics/${repoName}"
    issueTrackerUrl = "https://github.com/obsidiandynamics/${repoName}/issues"
    publicDownloadNumbers = true
    githubRepo = "obsidiandynamics/${repoName}"
    override = true
    publish = false
    version {
      name = project(":").version
      desc = packageDesc
      released  = new Date()
      vcsTag = project(":").version
    }
  }
}
