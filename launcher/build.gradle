def packageName = "fulcrum-launcher"
def packageDescription = "Launch applications from Gradle"

dependencies {
  api project(":fulcrum-func")
  api project(":fulcrum-props")
  api project(":fulcrum-resolver")

  testImplementation project(":fulcrum-assert")
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = packageName
      from components.java
      pom {
        name = packageName
        description = packageDescription
        url = 'https://github.com/obsidiandynamics/fulcrum'
        licenses {
          license {
            name = 'BSD 3-Clause License'
            url = 'https://opensource.org/licenses/BSD-3-Clause'
          }
        }
        developers {
          developer {
            id = 'ekoutanov'
            name = 'Emil Koutanov'
            email = 'ekoutanov@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/obsidiandynamics/fulcrum.git'
          developerConnection = 'scm:git:ssh://github.com/obsidiandynamics/fulcrum.git'
          url = 'https://github.com/obsidiandynamics/fulcrum'
        }
      }
    }
  }
}

project.ext.set("launcherClasspath", new org.gradle.api.internal.file.UnionFileCollection())
project.ext.set("launcherClasses", new ArrayList())

task addLauncherApps() {
  project(":fulcrum-launcher").launcherClasspath += sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
  project(":fulcrum-launcher").launcherClasses += "com.obsidiandynamics.launcher.RunSampleApp"
}

task launch() {
  dependsOn allprojects*.tasks*.findByName("testClasses")
  doLast {
    def baseJvmArgs = "-XX:-MaxFDLimit -XX:+TieredCompilation -XX:+UseNUMA -XX:+UseCondCardMark " + 
                      "-XX:-UseBiasedLocking -Xms2G -Xmx2G -Xss1M -XX:+UseG1GC -XX:MaxGCPauseMillis=200 " + 
                      "-XX:InitiatingHeapOccupancyPercent=0 -Djava.net.preferIPv4Stack=true " + 
                      "-Dlauncher.package.compress.level=3"
    javaexec {
      systemProperties = System.properties
      classpath = project(":fulcrum-launcher").launcherClasspath
      main = "com.obsidiandynamics.launcher.Launcher"
      args = project(":fulcrum-launcher").launcherClasses
      jvmArgs = Arrays.asList baseJvmArgs.split(" ")
      standardInput = System.in
    }
  }
}
