def packageName = "fulcrum-launcher"
version = project(":").version

dependencies {
  compile project(":fulcrum-classes")
  compile project(":fulcrum-func")
  compile project(":fulcrum-props")
  compile project(":fulcrum-resolver")
  testCompile project(":fulcrum-assert")
}

jar {
  baseName packageName
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  baseName packageName
  classifier = "javadoc"
  from "$buildDir/docs/javadoc"
}
 
task sourcesJar(type: Jar) {
  baseName packageName
  from sourceSets.main.allSource
  classifier = "sources"
}
 
artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project(":").group
      artifactId packageName
      version project(":").version
      from components.java
      artifact sourcesJar
      artifact javadocJar
    }
  }
  repositories {
    maven {
      url "${project(':').projectDir}/../repo"
    }
  }
}

def envUser = "BINTRAY_USER"
def envKey = "BINTRAY_KEY"
def packageDesc = "Launch applications from Gradle"
def repoName = "fulcrum"
bintray {
  user = System.getenv(envUser)
  key = System.getenv(envKey)
  publications = ["mavenJava"]
  pkg {
    repo = "${repoName}"
    name = packageName
    userOrg = "obsidiandynamics"
    desc = packageDesc
    websiteUrl = "https://github.com/obsidiandynamics/${repoName}"
    licenses = ["BSD New"]
    vcsUrl = "https://github.com/obsidiandynamics/${repoName}"
    issueTrackerUrl = "https://github.com/obsidiandynamics/${repoName}/issues"
    publicDownloadNumbers = true
    githubRepo = "obsidiandynamics/${repoName}"
    override = true
    publish = false
    version {
      name = project(":").version
      desc = packageDesc
      released  = new Date()
      vcsTag = project(":").version
    }
  }
}

project.ext.set("launcherClasspath", new org.gradle.api.internal.file.UnionFileCollection())
project.ext.set("launcherClasses", new ArrayList())

task addLauncherApps() {
  project(":fulcrum-launcher").launcherClasspath += sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
  project(":fulcrum-launcher").launcherClasses += "com.obsidiandynamics.launcher.RunSampleApp"
}

task launch() {
  dependsOn allprojects*.tasks*.findByName("testClasses")
  doLast {
    def baseJvmArgs = "-XX:-MaxFDLimit -XX:+TieredCompilation -XX:+UseNUMA -XX:+UseCondCardMark " + 
                      "-XX:-UseBiasedLocking -Xms2G -Xmx2G -Xss1M -XX:+UseG1GC -XX:MaxGCPauseMillis=200 " + 
                      "-XX:InitiatingHeapOccupancyPercent=0 -Djava.net.preferIPv4Stack=true " + 
                      "-Dlauncher.package.compress.level=3"
    javaexec {
      systemProperties = System.properties
      classpath = project(":fulcrum-launcher").launcherClasspath
      main = "com.obsidiandynamics.launcher.Launcher"
      args = project(":fulcrum-launcher").launcherClasses
      jvmArgs = Arrays.asList baseJvmArgs.split(" ")
      standardInput = System.in
    }
  }
}
